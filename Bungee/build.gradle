group = rootProject.group + '.bungee'
String rootDependencyDir = rootProject.group + '.dependencies'
String dependencyDir = group + '.dependencies'
version = rootProject.version
archivesBaseName = rootProject.name + '-Bungee'

repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    implementation "com.github.FrankHeijden.cloud:cloud-bungee:${rootProject.cloudVersion}"
    implementation 'net.kyori:adventure-api:4.8.1'
    implementation 'net.kyori:adventure-platform-bungeecord:4.0.0-SNAPSHOT'
    implementation 'org.bstats:bstats-bungeecord:2.2.1'
    implementation project(":Common")
    compileOnly 'net.md-5:bungeecord-api:1.17-R0.1-SNAPSHOT'
}

processResources {
    from('src/main/resources') {
        include 'bungee.yml'
        expand(version: project.version)
    }
}

shadowJar {
    relocate 'org.bstats', dependencyDir + '.bstats'
    relocate 'net.kyori.adventure', rootDependencyDir + '.adventure'
    relocate 'net.kyori.examination', rootDependencyDir + '.examination'
    relocate 'net.kyori.adventure.text.minimessage', dependencyDir + '.adventure.text.minimessage'
    relocate 'net.frankheijden.serverutils.common', dependencyDir + '.su.common'
}
