group = rootProject.group + '.bungee'
String rootDependencyDir = rootProject.group + '.dependencies'
String dependencyDir = group + '.dependencies'
version = rootProject.version
archivesBaseName = rootProject.name + '-Bungee'

repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    implementation "cloud.commandframework:cloud-bungee:${rootProject.cloudVersion}"
    implementation "net.kyori:adventure-api:${rootProject.adventureVersion}"
    implementation "net.kyori:adventure-platform-bungeecord:${rootProject.adventurePlatformVersion}"
    implementation ("net.kyori:adventure-text-minimessage:${rootProject.adventureMinimessageVersion}") {
        exclude group: 'net.kyori', module: 'adventure-api'
    }
    implementation "org.bstats:bstats-bungeecord:${rootProject.bstatsVersion}"
    implementation project(":Common")
    compileOnly 'net.md-5:bungeecord-api:1.17-R0.1-SNAPSHOT'
}

processResources {
    from('src/main/resources') {
        include 'bungee.yml'
        expand(version: project.version)
    }
}

shadowJar {
    relocate 'org.bstats', dependencyDir + '.bstats'
    relocate 'net.kyori.adventure', rootDependencyDir + '.adventure'
    relocate 'net.kyori.examination', rootDependencyDir + '.examination'
    relocate 'net.kyori.adventure.text.minimessage', dependencyDir + '.adventure.text.minimessage'
    relocate 'net.frankheijden.serverutils.common', dependencyDir + '.su.common'
}
