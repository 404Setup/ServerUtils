plugins {
    id 'net.kyori.blossom' version '1.3.0'
}

group = rootProject.group + '.velocity'
String dependencyDir = group + '.dependencies'
version = rootProject.version
archivesBaseName = rootProject.name + '-Velocity'

repositories {
    maven { url 'https://nexus.velocitypowered.com/repository/maven-public/' }
    maven { url 'https://libraries.minecraft.net' }
}

dependencies {
    implementation "com.github.FrankHeijden.cloud:cloud-velocity:${rootProject.cloudVersion}"
    implementation 'org.bstats:bstats-velocity:2.2.1'
    implementation project(":Common")
    implementation ('net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT') {
        exclude group: 'net.kyori', module: 'adventure-api'
    }
    compileOnly 'com.velocitypowered:velocity-api:3.0.0'
    compileOnly 'com.velocitypowered:velocity-brigadier:1.0.0-SNAPSHOT'
    compileOnly 'com.electronwill.night-config:toml:3.6.3'
    annotationProcessor 'com.velocitypowered:velocity-api:3.0.0'
}

shadowJar {
    relocate 'org.bstats', dependencyDir + '.bstats'
    relocate 'net.frankheijden.serverutils.common', dependencyDir + '.su.common'
    relocate 'net.kyori.adventure.text.minimessage', dependencyDir + '.adventure.text.minimessage'
}

blossom {
    replaceTokenIn('src/main/java/net/frankheijden/serverutils/velocity/ServerUtils.java')
    replaceToken '${version}', version
}
